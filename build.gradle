buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

group = "cofh"
archivesBaseName = "CoFHCore"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

repositories {
    maven {
        name = "ChickenBones repo"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name = "JEI Maven."
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    compile "codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:deobf"
    compile "codechicken:NotEnoughItems:${config.mc_version}-${config.nei_version}:deobf"
    compile "mezz.jei:jei_${config.mc_version}:${config.jei_version}"
}

version = "${project.config.mod_version}." + (System.getenv("BUILD_NUMBER") ?: "1")

println config.mc_version + "-" + config.forge_version
// Setup the forge minecraft plugin data. Specify the preferred forge/minecraft version here
minecraft {
    version = config.mc_version + "-" + config.forge_version
    mappings = config.mappings
    runDir = "run"
    replace '${mod_version}', project.config.mod_version
    makeObfSourceJar = false
}

processResources {
    inputs.property "version", project.version
    inputs.property "mc_version", project.config.mc_version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mc_version': project.config.mc_version
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

version = "${project.minecraft.version}-${project.version}"
def commonManifest = {
    attributes 'FMLAT': 'CoFH_at.cfg CoFHLib_at.cfg'
    attributes 'FMLCorePlugin': 'cofh.asm.LoadingPlugin'
    attributes 'FMLCorePluginContainsFMLMod': 'true'
}

jar {
    classifier = 'universal'
    manifest commonManifest
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest commonManifest
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    manifest commonManifest
}

// Tell the artifact system about our extra jars
artifacts {
    archives deobfJar
    archives srcJar
}

// Set custom sourceSets, allows us to pack Lib inside Core.
def CoFHLibDir = new File(project.projectDir, "CoFHLib/")

sourceSets.main.java.srcDir new File(CoFHLibDir, "src/main/java")
sourceSets.main.resources.srcDir new File(CoFHLibDir, 'src/main/resources')

// Configure an upload task.
uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    // Force the maven upload to use the <mcversion>-<version> syntax preferred at files
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'CoFHCore'
                        url 'https://github.com/TheCBProject/CoFHCore'

                        scm {
                            url 'https://github.com/TheCBProject/CoFHCore'
                            connection 'scm:git:git://github.com/TheCBProject/CoFHCore.git'
                            developerConnection 'scm:git:git@github.com:TheCBProject/CoFHCore.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/TheCBProject/CoFHCore/issues'
                        }

                        licenses {
                            license {
                                name "CoFH Don't Be a Jerk License"
                                url 'https://github.com/TheCBProject/CoFHCore/blob/master/README.md'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'TeamCoFH'
                                name 'TeamCoFH'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}
